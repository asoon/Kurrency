apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply from: '../jacoco.gradle'

android {
    compileSdkVersion 28
    testBuildType "mock"

    defaultConfig {
        applicationId "com.rain.currency"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 5
        versionName "1.0.4"

        buildConfigField "String", "BASE_URL", '"https://kurrency.herokuapp.com/api/"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled = true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            buildConfigField "String", "ACCESS_TOKEN", AccessToken
        }

        mock {
            initWith(debug)
            buildConfigField "String", "BASE_URL", '"http://localhost:8000/"'
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            buildConfigField "String", "ACCESS_TOKEN", AccessToken
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "Kurrency-${variant.versionName}.apk"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    // Support
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    // Crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }

    // Image Handling
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    // Dagger
    implementation 'com.google.dagger:dagger:2.22.1'
    implementation 'com.google.dagger:dagger-android:2.22.1'
    implementation 'com.google.dagger:dagger-android-support:2.22.1'
    kapt 'com.google.dagger:dagger-compiler:2.22.1'
    kapt 'com.google.dagger:dagger-android-processor:2.22.1'

    // Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.7'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    // Butter Knife
    implementation 'com.jakewharton:butterknife:10.1.0'
    kapt 'com.jakewharton:butterknife-compiler:10.1.0'

    // Networking
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.2'

    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.22.0'

    // Android test
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.12.2'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.1'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
